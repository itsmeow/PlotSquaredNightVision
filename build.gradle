buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'kr.entree:spigradle:1.2.4'
    }
}
apply plugin: 'kr.entree.spigradle'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileJava.options.encoding = "UTF-8"

group = project.maven_group
version "${project.project_version}"
archivesBaseName = project.project_name

repositories {
  mavenCentral()
  maven { url = "https://mvn.intellectualsites.com/content/groups/public/" }
  flatDir {
      dirs 'libs'
  }
}

dependencies {
  compileOnly spigot("${project.mc_version}")
  implementation files("libs/PlotSquared-Bukkit-4.4.495.jar")
}

spigot {
  authors = ['itsmeow']
  depends = ['PlotSquared']
  apiVersion = project.api_version
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
            pom {
                name = project.project_name
                url = project.url
                packaging = 'jar'
                scm {
                    connection = "scm:git:git://github.com/" + project.github + ".git"
                    developerConnection = "scm:git:git@github.com:" + project.github + ".git"
                    url = "https://github.com/" + project.github
                }
                issueManagement {
                    system = 'github'
                    url = "https://github.com/" + project.github + ".git/issues"
                }
                organization {
                    name = 'itsmeow Mods'
                    url = 'https://itsmeow.dev/'
                }
            }
        }
    }
    repositories {
        maven {
            if ((System.getenv("MAVEN_USER") != '') && (System.getenv("MAVEN_PASSWORD") != '')) {
                credentials {
                    username System.getenv("MAVEN_USER")
                    password System.getenv("MAVEN_PASSWORD")
                }
            }
            if (System.getenv("MAVEN_URL") != '') {
                url System.getenv("MAVEN_URL")
            } else {
                url "file:///${project.buildDir}/repo"
            }
        }
    }
}